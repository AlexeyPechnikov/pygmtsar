#!/bin/csh -f 
#       $Id$
#  D. Sandwell 1/12/07
#
alias rm 'rm -f'
unset noclobber
#
#
#  project a grd file from range/azimuth coordinates into lon/lat coordinates
#  this version only works with GMT V4.0 and higher
#
#  Input:
#  trans.dat    - file generated by llt_grid2rat  (r a topo lon lat)
#  phase_ra.grd - a GRD file of phase or anything
#
#  Output:
#  phase_ll.grd - a GRD file of phase in longitude/latitude coordinates
#
# check for number of arguments
#
 if ($#argv < 3) then
  echo " "
  echo "Usage: proj_ra2ll.csh trans.dat phase.grd phase_ll.grd" 
  echo "        trans.dat    - file generated by llt_grid2rat  (r a topo lon lat)"
  echo "        phase_ra.grd - a GRD file of phase or anything" 
  echo "        phase_ll.grd - output file in lon/lat-coordinates" 
  echo " "
  exit 1
 endif 
 echo "proj_ra2ll.csh"
#
#  extract the phase in the r a positions
#
gmt grd2xyz $2 -s -bo3f > rap
#
#   make grids of longitude and latitude versus range and azimuth unless they already exist
#
if (! -f raln.grd || ! -f ralt.grd ) then
  gmt gmtconvert $1 -o0,1,3 -bi5d -bo3f > raln
  gmt gmtconvert $1 -o0,1,4 -bi5d -bo3f > ralt
#
gmt surface raln `gmt gmtinfo rap -I16/32 -bi3f` -bi3f -I16/32 -T.50 -Graln.grd -V
gmt surface ralt `gmt gmtinfo rap -I16/32 -bi3f` -bi3f -I16/32 -T.50 -Gralt.grd -V
endif
#
gmt grdtrack rap -nl -Graln.grd -bi3f -bo4f > rapln
gmt grdtrack rapln -nl -Gralt.grd -bi4f -bo5f > raplnlt
#
# get the lon, lat, phase columns and grid
#
gmt gmtconvert raplnlt -bi5f -bo3f -o3,4,2 > llp
#
# use higher resolution for data with higher range resolution and PRF
#
set rng_samp_rate = `grep rng_samp_rate *.PRM | awk 'NR == 1 {printf("%d", $3)}'`
set PRF = `grep PRF *.PRM | awk 'NR == 1 {printf("%d", $3)}'`
if ($rng_samp_rate > 35000000 && $PRF > 1000 ) then
#
# use a 60 m grid
#
   gmt blockmedian llp `gmt gmtinfo llp -I20s -bi3f ` -bi3f -bo3f -I2s -r -V > llpb
   gmt xyz2grd llpb `gmt gmtinfo llpb -I20s -bi3f ` -I2s -r -fg -G$3 -bi3f
else
#
# use a 120 m grid
#
   gmt blockmedian llp `gmt gmtinfo llp -I40s -bi3f ` -bi3f -bo3f -I4s -r -V > llpb
   gmt xyz2grd llpb `gmt gmtinfo llpb -I40s -bi3f ` -I4s -r -fg -G$3 -bi3f
#
# could use a coarser lon lat grid of 240 m
#
#   gmt blockmedian llp `gmt gmtinfo llp -I80s -bi3f ` -bi3f -bo3f -I8s -r -V > llpb
#   gmt xyz2grd llpb `gmt gmtinfo llpb -I80s -bi3f ` -I8s -r -fg -G$3 -bi3f
#
# could use a coarser lon lat grid of 240 m at high latitude for larger lon spacing
#
#   gmt blockmedian llp `gmt gmtinfo llp -I160s/80s -bi3f ` -bi3f -bo3f -I16s/8s -r -V > llpb
#   gmt xyz2grd llpb `gmt gmtinfo llpb -I160s/80s -bi3f ` -I16s/8s -r -fg -G$3 -bi3f
endif
#
# clean
#
rm rap* llp llpb raln ralt
